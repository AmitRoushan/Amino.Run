apply plugin: 'java-library'

dependencies {
    compile project(':sapphire-core')
}

// Task for Stub compilation
task compileStubs(type: JavaCompile) {
    source = sourceSets.main.java.srcDirs
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.main.output.classesDir
    options.incremental = true
}

// Task for app stub generation
task genAppStubs(type: JavaExec) {
    main = "sapphire.compiler.StubGenerator"
    classpath = sourceSets.main.runtimeClasspath
    def pkgName = 'sapphire.appexamples.minnietwitter'
    def src = "$buildDir/classes/java/main/sapphire/appexamples/minnietwitter/"
    def dst = "src/main/java/sapphire/appexamples/minnietwitter/stubs/"
    args src, pkgName, dst
}

clean.doFirst {
    delete fileTree("$projectDir/src/main/java/sapphire/appexamples/minnietwitter/app/stubs") {
        include '**/*_Stub.java'
    }
    println "Deleted $projectDir/src/main/java/sapphire/appexamples/minnietwitter/app/stubs"
}

genAppStubs.mustRunAfter compileJava
compileStubs.dependsOn genAppStubs
jar.dependsOn compileStubs

task runoms(type: JavaExec) {
    dependsOn jar
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.oms.OMSServerImpl'
    args project.property('omsIp'), project.property('omsPort')
}

task runks(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.kernel.server.KernelServerImpl'
    args project.property('kernelServerIp'), project.property('kernelServerPort'), project.property('omsIp'), project.property('omsPort')
}

task runapp(type: JavaExec) {
    main = "sapphire.appexamples.minnietwitter.MinnieTwitterMain"
    classpath = sourceSets.main.runtimeClasspath
    args project.property('omsIp'), project.property('omsPort'), project.property('kernelIpAndroid'), project.property('kernelServerPort')
}
