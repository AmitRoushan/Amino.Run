plugins {
    id "com.google.protobuf" version "0.8.3"
    id "idea"
}

apply plugin: 'com.android.application'


android {
// Terry: It is safe to delete the following two settings. But
// I would like to keep them here as comment temporarily to 
// remind myself that it used to take parameters from parent
// projects. I will remove them eventually.
//    compileSdkVersion project.ext.compileSdkVersion.toInteger()
//    buildToolsVersion project.ext.buildToolsVersion
    compileSdkVersion=27
    buildToolsVersion="27.0.2"

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/INDEX.LIST'
    }

    defaultConfig {
        applicationId "com.example.minnietwitter"
        versionCode 1
        versionName "1.0"
        minSdkVersion 24
        targetSdkVersion 27
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    dexOptions {
        preDexLibraries = false
        additionalParameters = ["--core-library"]
    }
}

configurations {
    protoCompile
}

dependencies {
    compile 'com.android.support:support-v4:27.0.2'
    compile project(':sapphire-core')
}

// Added for gRPC and Protobuf compilation -->
repositories {
    mavenCentral()
}
dependencies {
    compile "com.google.protobuf:protobuf-lite:3.0.0"
    compile "javax.annotation:javax.annotation-api:1.2"
    compile 'io.grpc:grpc-protobuf-lite:1.9.0'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'io.protostuff:protostuff-core:1.3.7'
    compile 'io.protostuff:protostuff-runtime:1.3.7'
    protoCompile "io.protostuff:protostuff-cli:0-SNAPSHOT"
}
protobuf {
    protoc {
        // The version of protoc must match protobuf-java. If you don't depend on
        // protobuf-java directly, you will be transitively depending on the
        // protobuf-java version that grpc depends on.
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        javalite {
           artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
	grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.9.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite {}
                grpc { }
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
}

idea {
    module {
        sourceDirs += file("$projectDir/src/generated/main/grpc")
        sourceDirs += file("$projectDir/src/generated/main/java")
    }
}
// <-- End of changes for gRPC and Protobuf compilation.