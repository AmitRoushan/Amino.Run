apply plugin: 'java'

/*
    TODO: Add following changes after:
    1. Add task for generating and compiling stubs. (Currently manually added)
    2. genStubs is disabled below - enable it
*/
// Customize app stub generation for this example - disable it for now - see above.
genStubs {
    main = "sapphire.compiler.GraalStubGenerator"
    classpath =  files("$projectDir/../../sapphire-core/build/libs/sapphire-core-1.0.0.jar")

    def sapphireObjectPath = "$projectDir/src/main/ruby/sapphire/appexamples/hankstodo/todo_list_manager.rb"
    def outPath = "$projectDir/src/main/java"
    def packageName = "sapphire.appexamples.hankstodo.stubs"
    def sapphireClasses = "TodoListManager"
    //args sapphireObjectPath, outPath, packageName, sapphireClasses
    outputs.dir "$projectDir/src/main/java/sapphire/appexamples/hankstodo/stubs" // Declare outputs, so gradle will run if they have been changed
    inputs.dir sapphireObjectPath   // Declare inputs, so gradle will run if they have been changed
}
// Customize stub compilation for this example
compileStubs {
    outputs.dir "$buildDir/classes/java/main/sapphire/appexamples/hankstodo/stubs/"
}

jar.dependsOn compileStubs

// Task to run Ruby app
task runrubyapp(type: Exec) {
    environment  RUBY_HOME: "${project.projectDir}/src/main/ruby/sapphire/appexamples/hankstodo"
    environment  OMS_IP: "${project.property('omsIp')}"
    environment  OMS_PORT: "${project.property('omsPort')}"
    environment  KERNEL_SERVER_IP: "${project.property('embeddedKernelServerIp')}"
    environment  KERNEL_SERVER_PORT: "${project.property('kernelServerPort')}"
    def classpath = sourceSets.main.runtimeClasspath.asPath
    executable "$System.env.JAVA_HOME/bin/ruby"
    args "--jvm", "--polyglot", "--jvm.classpath=${classpath}",
            "${project.projectDir}/src/main/ruby/sapphire/appexamples/hankstodo/hanks_todo_main.rb"
}

// Task for run javascript hanksTodoApp demo app
task runjsapp(type: Exec) {
    environment  JS_HOME: "${project.projectDir}/src/main/js/sapphire/appexamples/hankstodo"
    environment  OMS_IP: "${project.property('omsIp')}"
    environment  OMS_PORT: "${project.property('omsPort')}"
    environment  HOST_IP: "${project.property('embeddedKernelServerIp')}"
    environment  HOST_PORT: "${project.property('kernelServerPort')}"
    def classpath = sourceSets.main.runtimeClasspath.asPath
    executable "$System.env.JAVA_HOME/bin/js"
    args "--jvm", "--polyglot", "--jvm.classpath=${classpath}",
            "${project.projectDir}/src/main/js/sapphire/appexamples/hankstodo/hanks_todo_main.js"
}

// Customise task to run Java app
runapp {
    systemProperty "RUBY_HOME",  "${project.projectDir}/src/main/ruby/sapphire/appexamples/hankstodo/"
    systemProperty "JS_HOME",  "${project.projectDir}/src/main/js/sapphire/appexamples/hankstodo/"
    main = 'sapphire.appexamples.hankstodo.Client'
    args = [project.property('embeddedKernelServerIp'), project.property('kernelServerPort'), project.property('omsIp'), project.property('omsPort')]
}

compileJava.dependsOn genStubs
runapp.dependsOn compileStubs
