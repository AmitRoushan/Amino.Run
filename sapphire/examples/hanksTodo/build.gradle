import dcap.sapphire.gradle.PolicyStubGenerationTask
apply plugin: 'java-library'

dependencies {
    compile project(':sapphire-core')
}

// Task for Stub compilation
task compileStubs(type: JavaCompile) {
    source = sourceSets.main.java.srcDirs
    classpath = sourceSets.main.compileClasspath
    destinationDir = sourceSets.main.output.classesDir
    options.incremental = true
}

// Task for app stub generation
task genhanksTodoAppStubs(type: JavaExec) {
    main = "sapphire.compiler.StubGenerator"
    classpath = sourceSets.main.runtimeClasspath
    def pkgName = 'sapphire.appexamples.hankstodo'
    def src = "$buildDir/classes/java/main/sapphire/appexamples/hankstodo"
    def dst = "src/main/java/sapphire/appexamples/hankstodo/stubs/"
    args src, pkgName, dst
}

genhanksTodoAppStubs.mustRunAfter compileJava
compileStubs.dependsOn genhanksTodoAppStubs
jar.dependsOn compileStubs

task runHanksTodoOms(type: JavaExec) {
    dependsOn jar
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.oms.OMSServerImpl'
    args project.property('omsIp'), project.property('omsPort')
}

task runHanksTodoKernelServer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'sapphire.kernel.server.KernelServerImpl'
    args project.property('kernelServerIp'), project.property('kernelServerPort'), project.property('omsIp'), project.property('omsPort')
}

// Task for run hanksTodoApp demo app
// Usage: gradlew runHanksTodoApp -PW=DisneyWorld
task runHanksTodoApp(type: JavaExec) {
    main = "sapphire.appexamples.hankstodo.hanskTododMain"
    classpath = sourceSets.main.runtimeClasspath
    args project.property('omsIp'), project.property('omsPort'), project.property('kernelIpAndroid'), project.property('kernelServerPort')
}