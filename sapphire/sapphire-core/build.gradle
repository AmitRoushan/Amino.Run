plugins {
  // Usage of googleJavaPlugin can be found at 
  // https://github.com/sherter/google-java-format-gradle-plugin
  id 'com.github.sherter.google-java-format' version '0.7.1'
}

googleJavaFormat {
  options style: 'AOSP'
}

dependencies {
    compile 'org.json:json:20171018'
    compile project(':dependencies:java.rmi')
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'org.powermock:powermock:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
}

compileJava.dependsOn tasks.googleJavaFormat

// Maven Plugin Properties
// Reset maven properties to ensure that their values are 
// consistent with the values used by Maven Publish Plugin
group = project.property('mavenGroupId')
version =  project.property('mavenVersion')

// run `gradle delstubs` to remove stub java files
task delstubs(type: Delete) {
    delete fileTree('src/main/java/sapphire/policy/stubs') {
        include '**/*_Stub.java'
    }
}

// TODO: We could use gradle to do automatic incremental builds.  
// Details can be found at https://blog.gradle.org/introducing-incremental-build-support
task genstubs(type: Exec) {
  dependsOn 'delstubs', 'build'
  executable "bash"
  args "-c", "pushd ../../generators && python generate_policy_stubs_on_host.py && popd"
}

task uberJar(type: Jar) {
    dependsOn ':dependencies:build'
    baseName = 'sapphire-core-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
